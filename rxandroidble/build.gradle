apply plugin: 'com.android.library'
apply plugin: 'groovyx.android'
apply plugin: 'com.vanniktech.maven.publish'
//apply from: rootProject.file('gradle/gradle-mvn-config.gradle')

android {
    namespace 'com.polidea.rxandroidble2'

    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 32
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.targetCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }

    lint {
        abortOnError false
    }

    preBuild.dependsOn 'checkstyle'
}

androidGroovy {
    skipJavaC = true
}

afterEvaluate { project ->
    // add explicit dependencies to compile groovy when testing
    testDebugUnitTest.dependsOn 'compileDebugUnitTestGroovyWithGroovyc'
    testReleaseUnitTest.dependsOn 'compileReleaseUnitTestGroovyWithGroovyc'
}

dependencies {
    api rootProject.ext.libs.rxjava2
    api rootProject.ext.libs.annotations
    implementation rootProject.ext.libs.rxrelay2
    implementation project(path: ':dagger-library-shadow', transitive: false, configuration: 'shadow')
    annotationProcessor project(path: ':dagger-compiler-shadow', transitive: false, configuration: 'shadow')

    compileOnly files(tasks.getByPath(':dagger-library-shadow:shadowJar').archiveFile)

    // Test dependencies
    testImplementation rootProject.ext.libs.junit
    testImplementation rootProject.ext.libs.groovy
    testImplementation rootProject.ext.libs.spock
}

afterEvaluate {
    publishing {
        publications.all { publication ->
            if (publication instanceof MavenPublication) {
                // 1. Remove artefatos de assinatura (.asc) da lista de artefatos
                var artifactsToRemove = publication.artifacts.matching { it.extension == 'asc' }
                publication.artifacts.removeAll(artifactsToRemove)

                // 2. Desativa a assinatura do POM (se houver tentativa de assinar o arquivo XML)
                // Isso remove a associação do arquivo .asc ao POM gerado
                if (publication.pom.hasProperty('ascFile')) {
                    publication.pom.ascFile = null
                }
            }
        }
    }

    // 3. Desativa explicitamente todas as tarefas de assinatura para garantir
    tasks.withType(Sign).configureEach {
        enabled = false
    }
}