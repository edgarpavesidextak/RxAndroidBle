apply plugin: 'com.android.library'
apply plugin: 'groovyx.android'
apply plugin: 'com.vanniktech.maven.publish'
//apply from: rootProject.file('gradle/gradle-mvn-config.gradle')

android {
    namespace 'com.polidea.rxandroidble2.mockrxandroidble'

    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 32
    }

    compileOptions {
        sourceCompatibility rootProject.ext.targetCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }

    lint {
        abortOnError false
    }

    preBuild.dependsOn 'checkstyle'

    sourceSets {
        test {
            java {
                srcDir("../rxandroidble/src/test/java") // Add android stubs from main lib tests
            }
        }
    }
}

afterEvaluate {
    testDebugUnitTest.dependsOn 'compileDebugUnitTestGroovyWithGroovyc'
    testReleaseUnitTest.dependsOn 'compileReleaseUnitTestGroovyWithGroovyc'
    // 1. Desativa a geração de metadados do Gradle (module.json)
    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }

    // 2. Desativa explicitamente as tarefas de assinatura
    tasks.withType(Sign).configureEach {
        enabled = false
    }

    // 3. Limpa a lista de artefatos da publicação
    publishing {
        publications.all { publication ->
            if (publication instanceof MavenPublication) {
                // Remove artefatos que terminam em .asc
                def artifactsToRemove = publication.artifacts.matching { it.extension == 'asc' }
                publication.artifacts.removeAll(artifactsToRemove)

                // Remove referência de assinatura no arquivo POM
                if (publication.pom.hasProperty('ascFile')) {
                    publication.pom.ascFile = null
                }
            }
        }
    }
}

dependencies {

    // Test dependencies
    testImplementation rootProject.ext.libs.junit
    testImplementation rootProject.ext.libs.groovy
    testImplementation rootProject.ext.libs.spock
    api project(path: ':rxandroidble')
    api rootProject.ext.libs.rxjava2
}
